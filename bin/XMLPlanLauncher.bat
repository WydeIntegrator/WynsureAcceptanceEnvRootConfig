@echo off

SET XMLPLANLAUNCHERCURRENTFOLDER=%CD%
cd /d %~dp0
set PLAN=%1
SET XMLPLANLAUNCHERRETURNCODE=0
SET LAUNCHBATCHERROR=95
SET FILENOTFOUNDERROR=97

CALL "eWAM Set Env.bat"
IF NOT %ERRORLEVEL%==0 SET XMLPLANLAUNCHERRETURNCODE=%ERRORLEVEL%
IF NOT %XMLPLANLAUNCHERRETURNCODE%==0 GOTO XMLPLANLAUNCHERERROR 

IF "%PLAN%"=="" GOTO LAUNCHERROR
IF NOT EXIST "%PLAN%" GOTO FILENOTFOUNDERROR


CALL %Wyde-Root%\bin\ewamconsole /ERRORMESSAGE:FALSE /DEBUGTRAP:FALSE /SYSTEM /RUNASIDE /INTEGRATION_XML_PLAN:%PLAN% /run:aWEXIntegrationLauncher.ExecutePlanFromXMLFile
IF NOT %ERRORLEVEL%==0 SET XMLPLANLAUNCHERRETURNCODE=%ERRORLEVEL%
IF NOT %XMLPLANLAUNCHERRETURNCODE%==0 GOTO XMLPLANLAUNCHERERROR 
GOTO END

:FILENOTFOUNDERROR
ECHO #ERROR# Plan xml file could not be found : %PLAN%. Please Check before retry.
SET XMLPLANLAUNCHERRETURNCODE=%FILENOTFOUNDERROR%
GOTO XMLPLANLAUNCHERERROR

:LAUNCHERROR
ECHO #ERROR# Launch error. Usage : %~nx0 XMLFILE
SET XMLPLANLAUNCHERRETURNCODE=%LAUNCHBATCHERROR%
GOTO XMLPLANLAUNCHERERROR

:XMLPLANLAUNCHERERROR
ECHO #ERROR# Plan launch ended in error with return code %XMLPLANLAUNCHERRETURNCODE% (xml file : %PLAN%)
GOTO END

:END
CD /D %XMLPLANLAUNCHERCURRENTFOLDER%
EXIT /B %XMLPLANLAUNCHERRETURNCODE%

